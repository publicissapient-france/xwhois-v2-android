buildscript {
    repositories {
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://maven.google.com" }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

dependencies {

    def support_version = '26.1.0'

    compile("com.android.support:appcompat-v7:$support_version",
            "com.android.support:cardview-v7:$support_version",
            "com.android.support:support-compat:$support_version",
            'io.reactivex.rxjava2:rxandroid:2.0.1',
            'io.reactivex.rxjava2:rxjava:2.1.5',
            'com.android.support.constraint:constraint-layout:1.0.2')

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

android {
    sourceSets { main.java.srcDirs += 'src/main/kotlin' }
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "fr.xebia.whois"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}